`ifndef __VERIF_PKG
`define __VERIF_PKG

`include "instr.pkg"

package verif;

    `ifdef SIMULATION

        typedef enum logic[1:0] {
            FETCH,
            UOP,
            FILL,
            UCROM
        } t_simid_spawn_point;

        typedef logic[95:0] t_simid_record;

        typedef struct packed {
            t_simid_record      record;
            t_simid_spawn_point spawn;
        } t_simid_key_struct;

        typedef union packed {
            t_simid_key_struct                   s;
            logic[$bits(t_simid_key_struct)-1:0] flat;
        } t_simid_key;

        typedef struct packed {
            int         fid;
            logic[63:0] pc;
            t_simid_key txid;
            t_simid_key pid;
            t_simid_key lid;
        } t_simid;

        `define SIMID_STRUCT t_simid SIMID;

        function string format_simid(t_simid simid);
            return $sformatf("txid:%05h pid:%05h lid:%05h", simid.txid, simid.pid, simid.lid);
        endfunction

        function t_simid_key make_simid_key(t_simid_record rec, t_simid_spawn_point spawn);
            make_simid_key.s.record = rec;
            make_simid_key.s.spawn  = spawn;
        endfunction

        `define SIMID_CREATE_RHS(SPAWN,FID,PC) \
            '{fid:FID, pc:PC, txid:make_simid_key(t_simid_record'(FID), SPAWN), pid: t_simid_key'('0), lid: make_simid_key(t_simid_record'(FID), SPAWN)}

        `define SIMID_SPAWN_RHS(PARENT,SPAWN,RECORD) \
            '{fid:PARENT.fid, pc:PARENT.pc, txid: PARENT.txid, pid: PARENT.lid, lid: make_simid_key(RECORD, SPAWN)}

        `define SIMID_SPAWN_CNTR(NAME,SPAWN_NOW,CLK,PARENT,SPAWN) \
            t_simid_record __simid_cntr_``NAME``; \
            initial __simid_cntr_``NAME`` = '0; \
            always_ff @(posedge CLK) if (SPAWN_NOW) __simid_cntr_``NAME`` <= __simid_cntr_``NAME`` + 1; \
            t_simid NAME; \
            assign NAME = `SIMID_SPAWN_RHS(PARENT,SPAWN,__simid_cntr_``NAME``);

    `else // SIMULATION

        `define SIMID_STRUCT

    `endif

endpackage

`endif // __VERIF_PKG
